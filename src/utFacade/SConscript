# HOW TO BUILD (A) LIBRAR-Y/IES FROM A MODULE
# Building libraries from modules should be consistent for each module.
# Use the following scheme for each new library created:
#
# a) Check for required libraries. If they are not available return False and export flags if needed
# b) Define the src-subdirectories for which the library should be compiled
#    and glob all files in there
# c) Define a [LIBRARY]_options variable for the library containing all dependencies
#    from other libraries. Create a clone from the master environment and add those options.
# d) WINDOWS ONLY: Create a header file which implements dll interface functionality
#    dependent from a [LIBRARY]_DLL define. If the define is set, the dll should be exported,
#    if not, the dll should be imported. Set the define in the preprocessor options for the environment.
# e) Build the library!
# f) Remove all entries for (at least) the LIBS and LIBPATH keys in the options and
#    add only the newly-compiled library as a dependcy. This will avoid multiple includes
#    of libraries. Also verify that the import/export-switch define is not set.
# g) Export the options so that other libraries can base on this library. 
# h) Optionally setup help and ide projects
#
# The use of options and possibility to export them makes hierarchical build environments
# obsolete. Avoid exporting new environments to the build system.

import sys
import os.path

have_freeglut = False
have_lapack = False
have_opencv = False
have_directshow = False
have_utvision = False
have_utdataflow = False

Import( '*' )

have_directshow = sys.platform == "win32"

# a)
if not have_utdataflow:
	print "utdataflow missing -- not building facade libraries"
	result = False;
	Return('result')

# create a config.h file, need environment for this
env = masterEnv.Clone()

haveMap = { True : '#define', False : '#undef' }
config_h_content = """#ifndef __UBITRACK_CONFIG_H_INCLUDED__
#define __UBITRACK_CONFIG_H_INCLUDED__
#define UBITRACK_COMPONENTS_PATH "%s"
#define UBITRACK_COMPONENTS_RELAVIVEPATH "%s"
#ifndef HAVE_GLEW 
%s HAVE_GLEW
#endif
#ifndef HAVE_FREEGLUT 
%s HAVE_FREEGLUT
#endif
#ifndef HAVE_LAPACK 
%s HAVE_LAPACK
#endif
#ifndef HAVE_OPENCV 
%s HAVE_OPENCV
#endif
#ifndef HAVE_COIN 
%s HAVE_COIN
#endif
#ifndef HAVE_DIRECTSHOW 
%s HAVE_DIRECTSHOW
#endif
#endif
""" % ( install_component_prefix.encode( 'string_escape' ), 'ubitrack', haveMap[False], haveMap[ have_freeglut ], haveMap[ have_lapack ], haveMap[ have_opencv ], haveMap[False], haveMap[ have_directshow ] )
env.CreateFile( os.path.join( Dir( '.' ).srcnode().abspath, 'Config.h' ), Value( config_h_content ) )

# b)
headers = globSourceFiles( '*.h' )
sources = globSourceFiles( '*.cpp' )

# c)
if have_utvision:
	utfacade_options = mergeOptions( utvision_all_options,  utdataflow_all_options)
else:
	utfacade_options = utdataflow_all_options.copy()
utfacade_options ['CPPPATH'] += [ os.path.join( getCurrentPath(), '../') ]

env.AppendUnique( **utfacade_options )


# d)
if sys.platform == 'win32':
	# On windows this statement will help the compiler to realize that it has to _export_ the library instead of importing it
	env.Append( CPPDEFINES = 'UTFACADE_DLL' )

# e)	
# facade library must be called "Ubitrack", otherwise old programs can't be used anymore and the advanced facade can't find the components directory on windows
libName = 'Ubitrack'
# compile all source files into single library
# {buildenvironment, source files, name of the library, build target}
libs = setupLibraryBuild(env, sources, libName, 'facade')


simpleHeaders = headers[:]
for src in [ "AdvancedFacade.h", "DataflowObserver.h", "SimpleApplicationPrivate.h" ]:
		simpleHeaders.remove( src );
		
setupIncludeInstall(env, simpleHeaders, 'utFacade', 'includes')

# f)
utfacade_all_options = utfacade_options.copy()
utfacade_options[ 'LIBS' ] = []
utfacade_options[ 'LIBPATH' ] = []
utfacade_options[ 'LIBS' ] += libs
utfacade_options[ 'LIBPATH' ] += [ install_library_prefix ]
utfacade_all_options[ 'LIBS' ] += libs
utfacade_all_options[ 'LIBPATH' ] += [ install_library_prefix ]


# g)
have_utfacade = libs != 0
Export( 'utfacade_options', 'have_utfacade', 'utfacade_all_options' )

# h)
generateHelp(utfacade_options)
createVisualStudioProject(env, sources, headers, 'utFacade')